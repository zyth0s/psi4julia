{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## DFT: GGA & Meta GGA Kernels"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Theory\n",
    "\n",
    "\n",
    "In density functional theory, we are interested in the energy expression:\n",
    "\n",
    "$$\n",
    "E_{xc} = D_{\\mu \\nu}^{T}(T_{\\mu \\nu}  + V_{\\mu \\nu}) + \\frac{1}{2} D_{\\mu \\nu }^{T} D_{\\lambda \\sigma}^T (\\mu \\nu|\\lambda \\sigma) + E_{xc}[\\rho_{\\alpha}({\\vec{r})}, \\rho_{\\beta}({\\vec{r})}]\n",
    "$$\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "Although the exchange correlation energy $E_{xc}$ is a functional of the density alone, the dependence on  $\\rho_{\\sigma}(\\vec{r})$ is highly non-local. Because of this, small variations of the densities may cause large variations of the exchange correlation potential $v_{xc}$. Additionally, $v_{xc}$ at a given point $\\vec{r}_i$ may be sensitive to changes at very distant points of $\\vec{r}_j$. \n",
    "\n",
    "\n",
    "\n",
    "In order to overcome this, both semilocal and nonlocal ingredients must be added to the energy density. As expressed on the grid, these include:\n",
    "\n",
    "The gradient of the density:\n",
    "$$\n",
    "g = |\\nabla \\rho(\\vec(r)) |\n",
    "$$\n",
    "The laplacian of the density:\n",
    "$$\n",
    "l = \\nabla^2 \\rho (\\vec{r})\n",
    "$$\n",
    "And the non-interacting kinetic energy density:\n",
    "$$\n",
    "\\tau = \\frac{1}{2} \\sum_k^{occ.} | \\nabla \\psi_k (\\vec{r}) |^2\n",
    "$$\n",
    "\n",
    "Different functional approximations are defined by what ingredients are required to be created. Here, we concentrate on GGAs and meta-GGAs:\n",
    "\n",
    "\n",
    "$$\n",
    "E_{xc}^{GGA}[\\rho] = \\int f(\\rho, g) \\cdot d\\vec{r}\n",
    "$$\n",
    "$$\n",
    "E_{xc}^{MGGA}[\\rho] = \\int f(\\rho, g, l, \\tau) \\cdot d\\vec{r}\n",
    "$$\n",
    "\n",
    "Where the integrands are known as the kernel, or the exchange-correlation energy density. It is clear here that, the more sophisticated density functional approximations, the more components are added to the exchange-correlation energy and potential. \n",
    "\n",
    "\n",
    "In practice we need to build a Kohn-Sham matrix:\n",
    "\n",
    "$$\n",
    "F_{\\mu \\nu}^{\\alpha} = H_{\\mu \\nu} + J_{\\mu \\nu} + V_{\\mu \\nu}^{xc},\n",
    "$$\n",
    "\n",
    "where the last therm is the exchange-correlation contribution $V_{\\mu \\nu}^{xc}$ that is defined as the functional derivative of the energy with respect to the density. \n",
    "\n",
    "\n",
    "$$\n",
    "V^{xc} = \\frac{\\partial E_{xc}}{\\partial D_{ab}} \n",
    "$$\n",
    "\n",
    "Once we have the $V^{xc}$ we can build the Kohn-Sham matrix and solve self consistently. \n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "PyObject <psi4.core.VBase object at 0x14faa42f0>"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using PyCall: pyimport\n",
    "psi4 = pyimport(\"psi4\")\n",
    "np   = pyimport(\"numpy\") # used only to cast to Julia Arrays\n",
    "\n",
    "mol = psi4.geometry(\"\"\"\n",
    "He\n",
    "\n",
    "symmetry c1\n",
    "\"\"\")\n",
    "psi4.set_options(Dict(\"BASIS\" => \"cc-pvdz\",\n",
    "                      \"DFT_SPHERICAL_POINTS\" => 6,\n",
    "                      \"DFT_RADIAL_POINTS\" => 5))\n",
    "\n",
    "psi4.core.set_output_file(\"output.dat\", false)\n",
    "\n",
    "gga_energy, gga_wfn = psi4.energy(\"PBE\", return_wfn=true)\n",
    "meta_energy, meta_wfn = psi4.energy(\"TPSS\", return_wfn=true)\n",
    "\n",
    "Vpot = gga_wfn.V_potential()\n",
    "Vpot_meta = meta_wfn.V_potential()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Building the GGA kernel\n",
    "\n",
    "\n",
    "From the LDA tutorial, we have seen how to obtain the density from the basis functions:\n",
    "\n",
    "$$\n",
    "\\rho_{\\sigma}(\\vec{r}) = D_{\\mu \\nu}^{\\sigma} \\phi_{\\mu}(\\vec{r}) \\phi_{\\nu}(\\vec{r}) \n",
    "$$\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "The GGA depends on the gradient of the density. Using a basis set it is calculated as:\n",
    "\n",
    "\n",
    "$$\n",
    "\\nabla_{\\sigma} \\rho{} (\\vec{r}) = 2  D_{\\mu \\nu}^{\\sigma}  \\phi_{\\mu}(\\vec{r}) \\nabla \\phi_{\\nu}(\\vec{r})\n",
    "$$ \n",
    "\n",
    "So that we can produce $\\gamma$:\n",
    "\n",
    "\n",
    "$$\n",
    "\\gamma_{\\alpha \\alpha}(\\vec{r}) = \\nabla \\rho_{\\alpha}(\\vec{r}) \\cdot \\nabla \\rho_{\\alpha}(\\vec{r})\n",
    "$$\n",
    "\n",
    "\n",
    "We then need to get the energy from the kernel by doing a numerical integration:\n",
    "$$e_{\\rm{xc}} = w^a f^a_{\\rm{xc}}$$\n",
    "\n",
    "Where the $w_{\\alpha}$ correspond to the combined Truetler and Lebedev weights at each point needed for the numerical quadrature.\n",
    "\n",
    "Finally, the potential on the grid will be given by the derivative of the kernel with respect to gamma:\n",
    "\n",
    "$$\n",
    "V^{\\gamma} = 2 \\frac{\\partial f}{\\partial \\gamma_{\\alpha\\alpha}} \\nabla \\rho_{\\alpha} + \\frac{\\partial f}{\\partial \\gamma_{\\alpha\\beta}} \\nabla \\rho_{\\beta}\n",
    "$$\n",
    "\n",
    "\n",
    "And it can be added and then it needs to get reintegrated back to atomic orbital space:\n",
    "\n",
    "$$\n",
    "V_{ab}^{\\gamma} = \\int_{\\mathbb{R}^3} V^{\\gamma} \\nabla (\\phi_{\\mu} \\phi_{\\nu}) d\\vec{r}\n",
    "$$\n",
    "\n",
    "The next calculation assumes that the density matrix $D$ is symmetric. This means that $ \\nabla \\phi(\\vec{r}) D_{\\mu \\nu} \\phi(\\vec{r})= \\phi(\\vec{r}) D_{\\mu \\nu} \\nabla \\phi(\\vec{r})$.\n",
    "One then ought to be careful with systems where this condition is not met, for example CPHF. \n",
    "\n",
    "Specifically,  here write this matrix contribution as:\n",
    "\n",
    "$$\n",
    "V_{ab}^{\\gamma} = 4 \\cdot \\nabla \\phi_{\\mu}(\\vec{r}) \\cdot V_{\\alpha \\alpha}^{\\gamma} \\cdot \\nabla n_{p}(\\vec{r}) \\cdot w_{\\alpha} \\cdot \\phi_{\\nu} (\\vec{r})\n",
    "$$\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "XC Energy -1.0433674766872723"
     ]
    },
    {
     "data": {
      "text/plain": [
       "5×5 Array{Float64,2}:\n",
       " -0.85644      -0.442878      2.49289e-18   0.0        1.4881e-18\n",
       " -0.442878     -0.386672      1.95373e-18   0.0       -5.20405e-18\n",
       "  2.49289e-18   1.95373e-18  -0.731914      0.0        0.0\n",
       "  0.0           0.0           0.0          -0.731914   0.0\n",
       "  1.4881e-18   -5.20405e-18   0.0           0.0       -0.731914"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "V matrix:\n",
      "\n",
      "Matches Psi4 V: true\n"
     ]
    }
   ],
   "source": [
    "#GGA Kernel\n",
    "\n",
    "D = np.array(gga_wfn.Da())\n",
    "\n",
    "V = zero(D)\n",
    "xc_e = 0.0\n",
    "\n",
    "rho = []\n",
    "points_func = Vpot.properties()[1]\n",
    "superfunc = Vpot.functional()\n",
    "\n",
    "xc_e, V = let xc_e = xc_e, rho=rho\n",
    "\n",
    "   # Loop over the blocks\n",
    "   for b in 1:Vpot.nblocks()\n",
    "       \n",
    "       # Obtain block information\n",
    "       block = Vpot.get_block(b-1)\n",
    "       points_func.compute_points(block)\n",
    "       npoints = block.npoints()\n",
    "       lpos = np.array(block.functions_local_to_global()) .+ 1\n",
    "       \n",
    "       # Obtain the grid weight\n",
    "       w = np.array(block.w())\n",
    "\n",
    "       # Compute phi!\n",
    "       phi = np.array(points_func.basis_values()[\"PHI\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       \n",
    "       phi_x = np.array(points_func.basis_values()[\"PHI_X\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       phi_y = np.array(points_func.basis_values()[\"PHI_Y\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       phi_z = np.array(points_func.basis_values()[\"PHI_Z\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       \n",
    "       # Build a local slice of D\n",
    "       lD = D[lpos, lpos]\n",
    "       \n",
    "       # Compute rho\n",
    "       # ρ[p] = ϕ[pμ] lD[μν] ϕ[pν]\n",
    "       rho = 2vec(sum( (phi * lD) .* phi, dims=2))\n",
    "       \n",
    "       # 2.0 for Px D P + P D Px, 2.0 for non-spin Density\n",
    "       # ∇ϕₓ[p] = 4 ϕ[pμ] lD[μν] ∇ₓϕ[pν] ∀ x,y,z\n",
    "       rho_x = 4vec(sum( (phi * lD) .* phi_x, dims=2))\n",
    "       rho_y = 4vec(sum( (phi * lD) .* phi_y, dims=2))\n",
    "       rho_z = 4vec(sum( (phi * lD) .* phi_z, dims=2))\n",
    "       gamma = @. rho_x^2 + rho_y^2 + rho_z^2\n",
    "       \n",
    "       inp = Dict()\n",
    "       inp[\"RHO_A\"] = psi4.core.Vector.from_array(rho)\n",
    "       inp[\"GAMMA_AA\"] = psi4.core.Vector.from_array(gamma)\n",
    "       \n",
    "       # Compute the kernel\n",
    "       ret = superfunc.compute_functional(inp, -1)\n",
    "       \n",
    "       # Compute the XC energy\n",
    "       vk = np.array(ret[\"V\"])[1:npoints]\n",
    "       xc_e += sum(w .* vk)\n",
    "           \n",
    "       # Compute the XC derivative.\n",
    "       v_rho_a = np.array(ret[\"V_RHO_A\"])[1:npoints]\n",
    "       # Vtmp[ab] = 0.5 ϕ[pb] Vρ_a[p] w[p] ϕ[pa]\n",
    "       Vtmp = 0.5phi' * (v_rho_a .* w .* phi)\n",
    "\n",
    "       #Comute gamma and its associated potential\n",
    "       v_gamma_aa = np.array(ret[\"V_GAMMA_AA\"])[1:npoints]\n",
    "       # Vtmp[ab] += 2 ∇ₓϕ[pb] Vγ_aa[p] ∇ₓρ[p] w[p] ϕ[pa] ∀ x,y,z\n",
    "       Vtmp += 2phi_x' * (v_gamma_aa .* rho_x .* w .* phi)\n",
    "       Vtmp += 2phi_y' * (v_gamma_aa .* rho_y .* w .* phi)\n",
    "       Vtmp += 2phi_z' * (v_gamma_aa .* rho_z .* w .* phi)\n",
    "\n",
    "\n",
    "       # Add the temporary back to the larger array by indexing, ensure it is symmetric\n",
    "       V[lpos, lpos] += (Vtmp + Vtmp')\n",
    "   end\n",
    "   xc_e, V\n",
    "end\n",
    "\n",
    "\n",
    "println(\"XC Energy \", xc_e)\n",
    "println(\"V matrix:\")\n",
    "display(V)\n",
    "\n",
    "println(\"\\nMatches Psi4 V: \", np.allclose(V, gga_wfn.Va()))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Building the meta-GGA kernel\n",
    "\n",
    "\n",
    "Just like we did with GGA, meta-GGA requires an extra component to be added. In this case is the kinetic energy density. \n",
    "\n",
    "$$\n",
    "\\tau_{\\sigma} (\\vec{r}) = D_{\\mu \\nu}^{\\sigma} \\nabla \\phi_{\\mu}(\\vec{r}) \\nabla \\phi_{\\nu}(\\vec{r})\n",
    "$$\n",
    "\n",
    "We calculate the $E_{xc}$ again with $ w^a f^a_{\\rm{xc}}$.\n",
    "\n",
    "And finally, the $\\tau$ potential contribution can be calculated as:\n",
    "\n",
    "$$\n",
    "V^{\\tau} = \\frac{\\partial f}{\\partial \\tau}\n",
    "$$\n",
    "\n",
    "Which is expressed as a matrix in the basis sest as:\n",
    "\n",
    "$$\n",
    "V_{\\mu \\nu}^{\\tau} = \\int_{\\mathbb{R}^3} V^{\\tau} \\nabla \\phi_{\\mu} \\nabla \\phi_{\\nu} d\\vec{r}\n",
    "$$\n",
    "\n",
    "\n",
    "In the code we calculate this contribution like so:\n",
    "\n",
    "$$\n",
    "V_{\\mu \\nu}^{\\tau} = \\frac{1}{2}  \\cdot \\nabla \\phi_{\\mu} \\cdot V^{\\tau}_{a} \\cdot w_{a} \\cdot \\nabla \\phi_{\\nu} \n",
    "$$\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5×5 Array{Float64,2}:\n",
       " -0.879544     -0.46382       1.41885e-17   0.0          -3.35009e-18\n",
       " -0.46382      -0.406835      2.07552e-18   0.0          -2.32601e-18\n",
       "  1.41885e-17   2.07552e-18  -0.699709      1.63704e-33   4.81482e-35\n",
       "  0.0           0.0           1.63704e-33  -0.699709      1.92593e-34\n",
       " -3.35009e-18  -2.32601e-18   4.81482e-35   1.92593e-34  -0.699709"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "XC Energy -1.0582048358062652\n",
      "V matrix:\n",
      "\n",
      "Matches Psi4 V: true\n"
     ]
    }
   ],
   "source": [
    "#meta-GGA Kernel\n",
    "\n",
    "D = np.array(meta_wfn.Da())\n",
    "\n",
    "V = zero(D)\n",
    "xc_e = 0.0\n",
    "\n",
    "\n",
    "rho = []\n",
    "points_func = Vpot_meta.properties()[1]\n",
    "superfunc = Vpot_meta.functional()\n",
    "\n",
    "xc_e, V = let xc_e = xc_e, rho=rho\n",
    "\n",
    "   # Loop over the blocks\n",
    "   for b in 1:Vpot.nblocks()\n",
    "       \n",
    "       # Obtain block information\n",
    "       block = Vpot.get_block(b-1)\n",
    "       points_func.compute_points(block)\n",
    "       npoints = block.npoints()\n",
    "       lpos = np.array(block.functions_local_to_global()) .+ 1\n",
    "       \n",
    "       tau = np.zeros(npoints)\n",
    "       \n",
    "       # Obtain the grid weight\n",
    "       w = np.array(block.w())\n",
    "\n",
    "       # Compute phi!\n",
    "       phi = np.array(points_func.basis_values()[\"PHI\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       \n",
    "       phi_x = np.array(points_func.basis_values()[\"PHI_X\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       phi_y = np.array(points_func.basis_values()[\"PHI_Y\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       phi_z = np.array(points_func.basis_values()[\"PHI_Z\"])[1:npoints, 1:size(lpos)[1]]\n",
    "       \n",
    "       # Build a local slice of D\n",
    "       lD = D[lpos, lpos]\n",
    "       \n",
    "       # Compute rho\n",
    "       # ρ[p] = 2 ϕ[pm] lD[mn] ϕ[pn]\n",
    "       rho = 2vec(sum( (phi * lD) .* phi, dims=2))\n",
    "       \n",
    "       # 2.0 for Px D P + P D Px, 2.0 for non-spin Density\n",
    "       # ∇ₓρ[p] = 4 ϕ[pm] lD[mn] ∇ₓϕ[pn]\n",
    "       rho_x = 4vec(sum( (phi * lD) .* phi_x, dims=2))\n",
    "       rho_y = 4vec(sum( (phi * lD) .* phi_y, dims=2))\n",
    "       rho_z = 4vec(sum( (phi * lD) .* phi_z, dims=2))\n",
    "       gamma = @. rho_x^2 + rho_y^2 + rho_z^2\n",
    "       \n",
    "       #Compute Tau\n",
    "       # τ[p] = ∇ₓϕ[pm] lD[mn] ∇ₓϕ[pn]\n",
    "       tau  = vec(sum( (phi_x * lD) .* phi_x, dims=2))\n",
    "       tau += vec(sum( (phi_y * lD) .* phi_y, dims=2))\n",
    "       tau += vec(sum( (phi_z * lD) .* phi_z, dims=2))\n",
    "       \n",
    "       inp = Dict()\n",
    "       inp[\"RHO_A\"] = psi4.core.Vector.from_array(rho)\n",
    "       inp[\"GAMMA_AA\"] = psi4.core.Vector.from_array(gamma)\n",
    "       inp[\"TAU_A\"]= psi4.core.Vector.from_array(tau)\n",
    "       \n",
    "       # Compute the kernel\n",
    "       ret = superfunc.compute_functional(inp, -1)\n",
    "       \n",
    "       # Compute the XC energy\n",
    "       vk = np.array(ret[\"V\"])[1:npoints]\n",
    "       xc_e += sum(w .* vk)\n",
    "           \n",
    "       # Compute the XC derivative.\n",
    "       v_rho_a = np.array(ret[\"V_RHO_A\"])[1:npoints]\n",
    "       # Vtmp[ab] = 0.5 ϕ[pb] Vρ_a[p] w[p] ϕ[pa]\n",
    "       Vtmp = 0.5phi' * (v_rho_a .* w .* phi)\n",
    "\n",
    "       \n",
    "       #Compute gamma and its potential matrix\n",
    "       v_gamma_aa = np.array(ret[\"V_GAMMA_AA\"])[1:npoints]\n",
    "       # Vtmp[ab] = 2∇ₓϕ[pb] Vγ_aa[p] ∇ₓϕ[p] w[p] ϕ[pa] ∀ x,y,z\n",
    "       Vtmp += 2phi_x' * (v_gamma_aa .* rho_x .* w .* phi)\n",
    "       Vtmp += 2phi_y' * (v_gamma_aa .* rho_y .* w .* phi)\n",
    "       Vtmp += 2phi_z' * (v_gamma_aa .* rho_z .* w .* phi)\n",
    "       \n",
    "       #Compute Vτ\n",
    "       v_tau_a = np.array(ret[\"V_TAU_A\"])[1:npoints]\n",
    "       # Vtmp[ab] += 0.5 ∇ₓϕ[pb] Vτ_a[p] w[p] ∇ₓϕ[pa] ∀ x,y,z\n",
    "       Vtmp += 0.5phi_x' * (v_tau_a .* w .* phi_x)\n",
    "       Vtmp += 0.5phi_y' * (v_tau_a .* w .* phi_y)\n",
    "       Vtmp += 0.5phi_z' * (v_tau_a .* w .* phi_z)\n",
    "\n",
    "       # Add the temporary back to the larger array by indexing, ensure it is symmetric\n",
    "       V[lpos, lpos] += (Vtmp + Vtmp')\n",
    "   end\n",
    "   xc_e, V\n",
    "end\n",
    "\n",
    "\n",
    "println(\"XC Energy \", xc_e)\n",
    "println(\"V matrix:\")\n",
    "display(V)\n",
    "\n",
    "println(\"\\nMatches Psi4 V: \", np.allclose(V, meta_wfn.Va()))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### To put all the approximations into perspective, let us look at every component of the meta-GGA exchange-correlation potential that we just created. \n",
    "\n",
    "$$\n",
    "V_{\\mu \\nu}^{xc, \\alpha} = \\int_{\\mathbb{R}^3} \\bigg( \\frac{\\partial f}{\\partial \\rho_{\\alpha}} \\bigg)  \\phi_{\\mu} \\phi_{\\nu} d\\vec{r}\n",
    "$$\n",
    "\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$$\n",
    "+\\int_{\\mathbb{R}^3} \\bigg(2 \\frac{\\partial f}{\\partial \\gamma_{\\alpha\\alpha}} \\nabla \\rho_{\\alpha} + \\frac{\\partial f}{\\partial \\gamma_{\\alpha\\beta}} \\bigg)  \\nabla \\rho_{\\beta} \\nabla (\\phi_{\\mu} \\phi_{\\nu}) d\\vec{r}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$$\n",
    "\\int_{\\mathbb{R}^3} \\bigg( \\frac{\\partial f}{\\partial \\tau} \\bigg)  \\nabla \\phi_{\\mu} \\nabla \\phi_{\\nu} d\\vec{r}\n",
    "$$\n",
    "    \n",
    "Here every line represent each of the rungs in the systematic methodology of density functional aproximations, the first line corresponds to LDA, addition of the second line corresponds to GGA and addition to the third line corresponds to meta-GGA."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## References\n",
    "\n",
    "1. Original papers:\n",
    "\t> [[Hohenberg:1964:136](https://journals.aps.org/pr/abstract/10.1103/PhysRev.136.B864)] P. Hohenberg and W. Kohn, Phys. Rev. 136, B864-B871, **1964**.\n",
    "    \n",
    "    > [[Kohn:1965:A1133](https://journals.aps.org/pr/abstract/10.1103/PhysRev.140.A1133)] W. Kohn and L.J. Sham, Phys. Rev. 140, A1133-A1138, **1965**.\n",
    "2. Analytic derivatives and algorithm:\n",
    "    > [[Johnson:1994:100](https://aip.scitation.org/doi/abs/10.1063/1.466887)] Johnson, B. G.; Fisch M. J.; *J. Chem. Phys.*, **1994**, *100*, 7429\n",
    "4. Additional information:\n",
    "\t> [[Staroverov:2012](https://onlinelibrary.wiley.com/doi/abs/10.1002/9781118431740#page=156)] Staroverov, Viktor N. \"Density-functional approximations for exchange and correlation.\" A Matter of Density, **2012**: 125-156.\n",
    "    \n",
    "    > [[Parr:1989](https://link.springer.com/chapter/10.1007/978-94-009-9027-2_2)] R.G. Parr and W. Yang, Density Functional Theory of Atoms and Molecules Oxford University Press, USA, 1989 ISBN:0195357736, 9780195357738"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "jl:light,ipynb"
  },
  "kernelspec": {
   "display_name": "Julia 1.4.2",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
